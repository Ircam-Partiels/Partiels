set(VAMP_SDK_DIRECTORY "${CMAKE_CURRENT_LIST_DIR}/vamp-plugin-sdk")
set(VAMP_HOST_SDK_DIRECTORY "${VAMP_SDK_DIRECTORY}/vamp-hostsdk")
set(VAMP_HOST_SOURCES_DIRECTORY "${VAMP_SDK_DIRECTORY}/src/vamp-hostsdk")
set(VAMP_PLUGIN_SOURCES_DIRECTORY "${VAMP_SDK_DIRECTORY}/src/vamp-sdk")

file(GLOB VAMP_HOST_SDK_HEADERS ${VAMP_HOST_SDK_DIRECTORY}/*h)
file(GLOB VAMP_HOST_SDK_SOURCES ${VAMP_HOST_SOURCES_DIRECTORY}/*h  ${VAMP_HOST_SOURCES_DIRECTORY}/*cpp ${VAMP_HOST_SOURCES_DIRECTORY}/*c)
source_group("Vamp" FILES ${VAMP_HOST_SDK_HEADERS} ${VAMP_HOST_SDK_SOURCES})
add_library(vamp STATIC ${VAMP_HOST_SDK_HEADERS} ${VAMP_HOST_SDK_SOURCES})
target_compile_definitions(vamp PUBLIC _VAMP_NO_HOST_NAMESPACE=1)
if(WIN32)
  target_compile_definitions(vamp PRIVATE _USE_MATH_DEFINES)
  target_compile_options(vamp PRIVATE /wd4996 /wd4267)
elseif(APPLE)
  set_target_properties(vamp PROPERTIES ARCHIVE_OUTPUT_DIRECTORY "./")
endif()
target_include_directories(vamp PUBLIC ${VAMP_SDK_DIRECTORY})

file(GLOB VAMP_PLUGIN_SOURCES ${VAMP_PLUGIN_SOURCES_DIRECTORY}/PluginAdapter.cpp ${VAMP_PLUGIN_SOURCES_DIRECTORY}/RealTime.cpp  ${VAMP_PLUGIN_SOURCES_DIRECTORY}/FFT.cpp)
source_group("Vamp/Plugin" FILES ${VAMP_PLUGIN_SOURCES})
add_library(vamp-plugin STATIC ${VAMP_PLUGIN_SOURCES})
if(WIN32)
  target_compile_definitions(vamp-plugin PRIVATE _USE_MATH_DEFINES)
  target_compile_options(vamp-plugin PRIVATE /wd4996 /wd4267)
  set_target_properties(vamp-plugin PROPERTIES MSVC_RUNTIME_LIBRARY "MultiThreaded")
elseif(APPLE)
  set_target_properties(vamp-plugin PROPERTIES ARCHIVE_OUTPUT_DIRECTORY "./")
else()
  set_property(TARGET vamp-plugin PROPERTY POSITION_INDEPENDENT_CODE ON)
endif()
target_include_directories(vamp-plugin PUBLIC ${VAMP_SDK_DIRECTORY})

set(VAMP_EXAMPLES_DIRECTORY "${VAMP_SDK_DIRECTORY}/examples")
file(GLOB VAMP_EXAMPLE_PLUGINS_SOURCES ${VAMP_EXAMPLES_DIRECTORY}/*h  ${VAMP_EXAMPLES_DIRECTORY}/*cpp ${VAMP_EXAMPLES_DIRECTORY}/*c)
source_group("Vamp/Examples" FILES ${VAMP_EXAMPLE_PLUGINS_SOURCES})
add_library(vamp-example-plugins SHARED ${VAMP_EXAMPLE_PLUGINS_SOURCES})
target_link_libraries(vamp-example-plugins PRIVATE vamp-plugin)
set_target_properties(vamp-example-plugins PROPERTIES PREFIX "")
set_target_properties(vamp-example-plugins PROPERTIES LIBRARY_OUTPUT_DIRECTORY_DEBUG ${CMAKE_CURRENT_BINARY_DIR}/Debug)
set_target_properties(vamp-example-plugins PROPERTIES LIBRARY_OUTPUT_DIRECTORY_RELEASE ${CMAKE_CURRENT_BINARY_DIR}/Release)
if(WIN32)
  set_target_properties(vamp-example-plugins PROPERTIES LINK_FLAGS "/export:vampGetPluginDescriptor")
  target_compile_options(vamp-example-plugins PRIVATE /wd4244 /wd4267 /wd4996)
endif()
